apply plugin: 'groovy'
apply plugin: 'bintray'

group = 'org.sonar.report.pdf.gradle'
description = 'Sonar PDF Report (Gradle plugin)'
version = '0.2'

buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    compile gradleApi()
    compile group: 'com.lowagie', name: 'itext', version:'2.1.4'
    compile group: 'dom4j', name: 'dom4j', version:'1.6.1'
    compile(group: 'jaxen', name: 'jaxen', version:'1.1.1') {
        exclude(module: 'icu4j')
    }
    compile group: 'commons-httpclient', name: 'commons-httpclient', version:'3.1'

    testCompile localGroovy()
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
}

ext.artifactBaseName = 'sonarpdf-plugin'

task sourcesJar(type: Jar){
    baseName artifactBaseName
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc){
    baseName artifactBaseName
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

jar {
    baseName artifactBaseName

    manifest {
        attributes 'Implementation-Title': 'Gradle SonarPDF plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty("user.name"),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty("java.version")
    }
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

apply plugin: 'maven-publish'

// Use publishGsheetsPublicationToMavenLocal task to publish to Maven local repo
publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

bintrayUpload.dependsOn jar

ext.myBintray = [ apiKey: bintrayApiKey ]   // Set 'binTrayAPIKey' in ~/.gradle/gradle.properties

bintray {
    user = 'sion5'
    key = myBintray.apiKey
    publications = ['plugin'] // When uploading Maven-based publication files
    pkg {
        repo = 'gradle-plugins'
        name = 'sonarpdf-gradle-plugin'
        desc = 'SNAPSHOT version of sonarpdf-gradle-plugin'
        labels = ['gradle', 'groovy', 'sonarpdf']
        licenses = ['Apache-2.0']
    }
    dryRun = false // whether to run this as dry-run, without deploying
}