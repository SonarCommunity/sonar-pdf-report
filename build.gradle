buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'bintray'
apply plugin: 'maven-publish'

group = 'org.sonar.report.pdf.gradle'
description = 'Sonar PDF Report (Gradle plugin)'
version = '0.3'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    compile gradleApi()
    compile group: 'com.lowagie', name: 'itext', version:'2.1.4'
    compile group: 'dom4j', name: 'dom4j', version:'1.6.1'
    compile(group: 'jaxen', name: 'jaxen', version:'1.1.1') {
        exclude(module: 'icu4j')
    }
    compile group: 'commons-httpclient', name: 'commons-httpclient', version:'3.1'

    testCompile localGroovy()
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
}

ext.artifactBaseName = 'sonarpdf-plugin'

task sourcesJar(type: Jar){
    baseName artifactBaseName
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc){
    baseName artifactBaseName
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle sonarpdf plugin')
                root.appendNode('description', 'Gradle plugin for generating pdf code quality report.')
                root.appendNode('inceptionYear', '2014')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/willis7/sonar-pdf-report')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'GPLv2')
                license.appendNode('url', 'https://www.gnu.org/licenses/gpl-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def willis7 = developers.appendNode('developer')
                willis7.appendNode('id', 'willis7')
                willis7.appendNode('name', 'Sion Williams')
                willis7.appendNode('email', 'sion5@hotmail.co.uk')
            }
        }
    }
}

bintrayUpload.dependsOn jar

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        name = 'sonarpdf-gradle-plugin'
        desc = 'SNAPSHOT version of sonarpdf-gradle-plugin'
        labels = ['gradle', 'groovy', 'sonarpdf']
        licenses = ['GPL-2.0']
    }
    dryRun = false // whether to run this as dry-run, without deploying
}